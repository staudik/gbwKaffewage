.program encoder

//handes the encoder

.wrap_target

.origin 0        ; The jump table has to start at 0
                 ; it contains the correct jumps for each of the 16  
                 ; combination of 4 bits formed by A'B'AB
                 ; A = current reading of pin_A of the rotary encoder
                 ; A' = previous reading of pin_A of the rotary encoder
                 ; B = current reading of pin_B of the rotary encoder
                 ; B' = previous reading of pin_B of the rotary encoder
    jmp read     ; 0000 = from 00 to 00 = no change in reading
    jmp read     ; 0001 = from 00 to 01 = clockwise rotation
    jmp read     ; 0010 = from 00 to 10 = counter clockwise rotation
    jmp read     ; 0011 = from 00 to 11 = error

    jmp read      ; 0100 = from 01 to 00 = counter clockwise rotation
    jmp read     ; 0101 = from 01 to 01 = no change in reading 
    jmp read     ; 0110 = from 01 to 10 = error
    jmp read       ; 0111 = from 01 to 11 = clockwise rotation
 
    jmp read       ; 1000 = from 10 to 00 = clockwise rotation
    jmp read     ; 1001 = from 10 to 01 = error
    jmp read     ; 1010 = from 10 to 10 = no change in reading 
    jmp read      ; 1011 = from 10 to 11 = counter clockwise rotation
 
    jmp read     ; 1100 = from 11 to 00 = error
    jmp CCW      ; 1101 = from 11 to 01 = counter clockwise rotation
    jmp CW       ; 1110 = from 11 to 10 = clockwise rotation
    jmp read     ; 1111 = from 11 to 11 = no change in reading 

pc_start:        ; this is the entry point for the program
    in pins 2    ; read the current values of A and B and use 
                 ; them to initialize the previous values (A'B')
read:
    mov OSR ISR  ; the OSR is (after the next instruction) used to shift 
                 ; the two bits with the previous values into the ISR
    out ISR 2    ; shift the previous value into the ISR. This also sets
                 ; all other bits in the ISR to 0
    in pins 2    ; shift the current value into the ISR
                 ; the 16 LSB of the ISR now contain 000000000000A'B'AB
                 ; this represents a jmp instruction to the address A'B'AB 
    mov exec ISR ; do the jmp encoded in the ISR
CW:              ; a clockwise rotation was detected
    irq 0        ; signal a clockwise rotation via an IRQ
    jmp read     ; jump to reading the current values of A and B
CCW:             ; a counter clockwise rotation was detected
    irq 1        ; signal a counter clockwise rotation via an IRQ
    jmp read     ; jump to reading the current values of A and B.
                 ; the jmp isn't needed because of the .wrap, and the first 
                 ; statement of the program happens to be a jmp read
    
    
    


.wrap





%c-sdk {
    //Helper function
    /*init the Statmachin for the interface for the hx711
    @param pio (PIO): pio instanc Ppio0/pio1
    @param sm (uint): claim sm
    @param offset (uint): offset of sm program
    @param SCLK (uint): Clock pin of hx711
    @param Dout (uint): data pin of hx711
    */
    static inline void encoder_program_init(PIO pio, uint sm, uint offset, uint a, uint b){
        
        pio_sm_config c = encoder_program_get_default_config(offset);
        
        pio_gpio_init(pio, a);    //Allow PIO to control GPIO pin
        gpio_set_pulls(a, true, false);
        pio_gpio_init(pio, b);
        gpio_set_pulls(a, true, false);

        sm_config_set_in_pins(&c, a);  //connect Dout to in pin

        sm_config_set_in_shift(&c, false, false, 0);

        pio1_hw->inte0 = PIO_IRQ0_INTE_SM0_BITS | PIO_IRQ0_INTE_SM1_BITS;

        pio_sm_init(pio, sm, offset, &c);

    }
%}